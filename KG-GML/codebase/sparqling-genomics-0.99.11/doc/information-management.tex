\chapter{Information management with SPARQL}

  In chapter \refer{chap:information-retrieval} we discussed how to ask
  questions to SPARQL endpoints.  In this chapter we will look at how we can
  modify the data in SPARQL endpoints.

  Using SPARQL, we can write ``layer 1'' programs --- programs that use RDF,
  and generate more RDF.

  Like the queries from chapter \refer{chap:information-retrieval}, the
  examples can be readily used in the query editor of the web interface (see
  chapter \refer{chap:web-interface}).

\section{Managing data in graphs}

  A simple way to subset data is to put triples in separate graphs.  When
  uploading RDF data to an RDF store, we must provide a graph name, so this
  sort of works by default.

  Sometimes we'd like to remove a graph altogether to make space for new
  datasets.  For this purpose we can use the \texttt{CLEAR GRAPH} query:

\begin{siderules}
\begin{verbatim}
CLEAR GRAPH <http://example>
\end{verbatim}
\end{siderules}

  After executing this query, all triples in the graph identified by
  \texttt{<http://example>} will be sent to a pieceful place where they
  cannot be accessed anymore.

  The \texttt{CLEAR GRAPH} query is equivalent to the more elaborate:

\begin{siderules}
\begin{verbatim}
DELETE { ?s ?p ?o }
FROM <http://example>
WHERE { ?s ?p ?o }
\end{verbatim}
\end{siderules}

  Using the \texttt{DELETE} construct, we can be more specific about which
  triples to remove from a graph by filling in one of the variables.

\section{Storing inferences in new graphs}
\label{sec:storing-inferences-in-new-graphs}

  Calculating inferences from a large amount of data can take a lot of time.
  To avoid calculating inferences over and over again, we can store the
  inferred information as triples.  The following example attempts to infer
  the gender related to a sample by looking at whether there's a mutation on
  the Y-chromosome.

\begin{siderules}
\begin{verbatim}
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX sg:  <http://sparqling-genomics/>
PREFIX faldo: <http://biohackathon.org/resource/faldo#>
PREFIX hg19:  <http://rdf.biosemantics.org/data/genomeassemblies/hg19#>

SELECT DISTINCT ?sample
FROM <http://hmf/variant_calling>
WHERE {
  ?sample   rdf:type        sg:Sample .
  ?variant  sg:sample       ?sample ;
            faldo:reference hg19:chrY .
}
\end{verbatim}
\end{siderules}

 Each \texttt{sample} returned by this query must've originated from a male
 donor, because it has a Y-chromosome (and also a mutation on the
 Y-chromosome).  Note that we cannot distinguish between females and males
 without a mutation on the Y-chromosome with this data, so we cannot accurately
 determine the gender for other samples.

 For the samples that definitely originated from a male donor (according to
 this inference rule), we can add a triplet in the form:

\begin{siderules}
\begin{verbatim}
   <sample-URI>  sg:gender sg:Male .
\end{verbatim}
\end{siderules}

  To do so, we use the \texttt{INSERT} construct:

\begin{siderules}
\begin{verbatim}
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX sg:  <http://sparqling-genomics/>
PREFIX faldo: <http://biohackathon.org/resource/faldo#>
PREFIX hg19:  <http://rdf.biosemantics.org/data/genomeassemblies/hg19#>

INSERT {
  GRAPH <http://meta> {
    ?sample   sg:gender        sg:Male .
  }
}
WHERE {
  GRAPH <http://hmf/variant_calling> {
    ?sample   rdf:type         sg:Sample .
    ?variant  sg:sample        ?sample ;
              faldo:reference  hg19:chrY .
  }
}
\end{verbatim}
\end{siderules}

  After which we can query for samples that definitely originated from a male
  donor using the following query:

\begin{siderules}
\begin{verbatim}
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX sg:  <http://sparqling-genomics/>
PREFIX faldo: <http://biohackathon.org/resource/faldo#>
PREFIX hg19:  <http://rdf.biosemantics.org/data/genomeassemblies/hg19#>

SELECT (COUNT (DISTINCT ?sample)) AS ?samples
FROM <http://hmf/variant_calling>
FROM <http://meta>
WHERE {
  ?sample  rdf:type   sg:Sample ;
           sg:gender  sg:Male .
}
\end{verbatim}
\end{siderules}

  The meaning of inferences is oftentimes limited in scope.  For example,
  inferring the gender by looking for mutations on the Y-chromosome works
  as long as the sequence mapping program did not accidentally map a read
  to the reference Y-chromosome because the X and Y chromosomes share
  homologous regions \citep{El-Mogharbel2008}.

  We therefore recommend keeping inferences (layer 1) in separate graphs
  than observed data (layer 0) because it allows users to choose which
  inferences are safe to apply in a particular case.

%  Inferences can be drawn from inferences, which can be classified as
%  ``layer 2'' or up.  The dependency graph of individial inference programs
%  can be used as a measure for the complexity of the knowledge graph.

\section{Foreign information gathering and SPARQL}

  The inference example in section \refer{sec:storing-inferences-in-new-graphs}
  was able to create information without needing additional data that isn't
  described as triples.

  Additional insights may require a combination of RDF triples and foreign
  data.  In such cases, a general-purpose programming language and SPARQL can
  form a symbiosis.  To display such a symbiosis, the following example uses
  the output of \texttt{vcf2rdf} to find out which samples belong to which
  user, by looking at the originating filenames.

  Furthermore, the example uses \texttt{guile-sparql} to interact with the
  SPARQL endpoint and GNU Guile as general-purpose programming language.

\begin{siderules}
\begin{verbatim}
(use-modules (sparql driver)
             (sparql util)
             (sparql lang))

(define %output-directory "/data/output")
(define %query "
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX sg: <http://sparqling-genomics/>
PREFIX vcf2rdf: <http://sparqling-genomics/vcf2rdf/>

SELECT ?origin ?filename
WHERE {
  ?sample rdf:type         sg:Sample ; sg:foundIn  ?origin .
  ?origin vcf2rdf:filename ?filename .
}")

(define (gather-ownership-info)
  (let* (;; Gather the origins and filenames from the SPARQL endpoint.
         (origins (query-results->list (sparql-query %query)))

         ;; We are going to store triples in this file.
         (ownership-file (string-append %output-directory "/ownership.n3"))

         ;; Define ontology prefixes.
         (rdf        (prefix "http://www.w3.org/1999/02/22-rdf-syntax-ns#"))
         (sg         (prefix "http://sparqling-genomics/"))
         (vcf2rdf    (prefix "http://sparqling-genomics/vcf2rdf/"))
         (user       (prefix "http://sparqling-genomics/User/"))
         (user-class "<http://sparqling-genomics/User>")
         (owner-pred "<http://sparqling-genomics/owner>"))

  ;; Generate triples for each entry.
  (call-with-output-file ownership-file
    (lambda (port)
      (for-each (lambda (entry)
                  ;; Extract the username of a file.
                  (let ((owner-name (passwd:name
                                      (getpwuid
                                        (stat:uid (stat (cadr entry)))))))
                  ;; Write RDF triples to the file.
                  (format port "~a ~a ~a .~%"
                               (user owner-name) (rdf "type") user-class)
                  (format port "<~a> ~a ~a .~%"
                               (car entry) owner-pred user-class)))
                (cdr origins))))))

(gather-ownership-info)
\end{verbatim}
\end{siderules}

  For a small amount of files, we could directly execute an \texttt{INSERT}
  query on the SPARQL endpoint, however, for a large amount of files we may
  want to use the RDF store's data loader for better performance.

  This program provides the triples that enables us to find which user
  contributed which variant call data in the graph:

\begin{siderules}
\begin{verbatim}
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX sg:  <http://sparqling-genomics/>
PREFIX vcf2rdf:  <http://sparqling-genomics/vcf2rdf/>

SELECT DISTINCT ?sample ?filename ?user
FROM <http://hmf/variant_calling>
FROM <http://ownership> # Assuming we the imported data into this graph
WHERE {
  ?sample   rdf:type         sg:Sample ;
            sg:foundIn       ?origin   .

  ?origin   vcf2rdf:filename ?filename ;
            sg:owner         ?user     .
}
\end{verbatim}
\end{siderules}
